apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'testdroid'
apply plugin: "androidx.navigation.safeargs"

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
        google()
    }

    dependencies {
        def navVersion = '2.1.0'

        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.testdroid:gradle:1.3.0'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navVersion"
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
        google()
    }
}

android {
    compileSdkVersion 28
    flavorDimensions "tier"

    defaultConfig {
        applicationId "bbct.android.common"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 22
        versionName "0.6.8.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
            debuggable true
        }
    }
    productFlavors {
        lite {
            dimension "tier"
            applicationId "bbct.android"
        }
        premium {
            dimension "tier"
            applicationId "bbct.android.premium"
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

testdroid {
    mode 'FULL_RUN'
    projectName 'BBCT Lite'

    fullRunConfig {
        instrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'
    }
}

Properties props = new Properties()
def propFile = new File('bbct.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null) {
        if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD')
                && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'bbct.properties found but SIGNING entries are missing'
            android.buildTypes.release.signingConfig = null
        }

        if (props.containsKey('TESTDROID_USERNAME') && props.containsKey('TESTDROID_PASSWORD')) {
            testdroid.username = props['TESTDROID_USERNAME']
            testdroid.password = props['TESTDROID_PASSWORD']
        } else {
            println 'bbct.properties found but TESTDROID entries are missing'
        }

        if (props.containsKey('TESTDROID_PACKAGE')) {
            testdroid.fullRunConfig.limitationType = 'PACKAGE'
            testdroid.fullRunConfig.limitationValue = props['TESTDROID_PACKAGE']
            println 'Setting test package: ' + props['TESTDROID_PACKAGE']
        }

        if (props.containsKey('TESTDROID_CLASS')) {
            testdroid.fullRunConfig.limitationType = 'CLASS'
            testdroid.fullRunConfig.limitationValue = props['TESTDROID_CLASS']
            println 'Setting test class: ' + props['TESTDROID_CLASS']
        }

        if (props.containsKey('TESTDROID_DEVICE_GROUP')) {
            testdroid.deviceGroup = props['TESTDROID_DEVICE_GROUP']
        } else {
            println '\'deviceGroup\' not specified'
        }
    }
} else {
    println 'bbct.properties not found'
}

ext {
    appcompatVersion = '1.1.0'
    fragment_version = "1.1.0"
    archCompVersion = '1.1.1'
    roomVersion = '2.1.0-alpha04'
    lifecycleVersion = '2.1.0-alpha02'
    butterknifeVersion = '10.0.0'
    espressoVersion = '3.1.1'
    testLibVersion = '1.1.1'
    uiautomatorVersion = '2.2.0'
    annotationVersion = '1.0.0'
    navVersion = '2.1.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':lib')
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "com.google.android.material:material:1.0.0-beta01"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta2"
    implementation "androidx.navigation:navigation-fragment:$navVersion"
    implementation "androidx.navigation:navigation-ui:$navVersion"
    implementation "androidx.room:room-runtime:${roomVersion}"
    implementation "androidx.lifecycle:lifecycle-livedata:${lifecycleVersion}"
    implementation 'com.google.android.gms:play-services:6.1.71'
    implementation('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }
    implementation "com.jakewharton:butterknife:${butterknifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"
    annotationProcessor "androidx.room:room-compiler:${roomVersion}"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation "androidx.annotation:annotation:${annotationVersion}"
    androidTestImplementation "androidx.test:runner:${testLibVersion}"
    androidTestImplementation "androidx.test:rules:${testLibVersion}"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:${uiautomatorVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"
    androidTestImplementation "androidx.fragment:fragment-testing:${fragment_version}"
    testImplementation "androidx.room:room-testing:${roomVersion}"
}
